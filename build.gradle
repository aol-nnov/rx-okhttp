plugins {
    id 'net.researchgate.release' version '2.3.3'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

group = 'com.shekhargulati.reactivex'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'io.reactivex:rxjava:1.0.15'
    compile 'org.apache.httpcomponents:httpclient:4.3.5'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.51'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.google.code.gson:gson:2.3.1'
    runtime 'org.slf4j:slf4j-simple:1.7.12'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

uploadArchives {
    repositories {
        mavenDeployer {
            ossrhUser = System.getenv('OSS_USER')
            ossrhPassword = System.getenv('OSS_PASSWORD')
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }

            pom.project {
                name 'Rx OkHttp'
                packaging 'jar'
                artifactId 'rx-okhttp'
                description 'A think RxJava wrapper around OKHttp'
                url 'https://github.com/shekhargulati/rx-okhttp'

                scm {
                    connection 'https://github.com/shekhargulati/rx-okhttp.git'
                    developerConnection 'https://github.com/shekhargulati/rx-okhttp.git'
                    url 'https://github.com/shekhargulati/rx-okhttp.git'
                }

                licenses {
                    license {
                        name 'MIT'
                    }
                }

                developers {
                    developer {
                        id 'shekhargulati'
                        name 'Shekhar Gulati'
                        email 'shekhargulati84@gmail.com'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}
